confirmation-view{
  match: Confirmation{
    confirming: CommitRecipeSearch(action)
  }
  message{
    template-macro (RecipeListMacro){
      param (recipeCommitState){
        expression (action.recipeCommitState)
      }
    }
  }
  mode (Transactional)
  confirm-options{
    label("선택")
    on-confirm{
      if(size(action.recipeCommitState.recipeBasicStructures) > 1){
        halt{
          dialog("레시피를 하나만 선택해주세요")
        }
      }
    }
  }
  render{
    layout{ 
      if(size(action.recipeCommitState.recipeBasicStructures) > 1){
        section{
          content{
            for-each(action.recipeCommitState.recipeBasicStructures){
              as(item){
                
                layout-macro (RecipeSummaryLarge){
                  param(recipeBasicStructure){
                    expression (item)
                  }
                }
                layout-macro (RecipeDetailName){
                  param(name){
                    expression (item.recipeName)
                  }
                }
                layout-macro (RecipeDetailSummary){
                  param(summary){
                    expression (item.summary)
                  }
                }
                layout-macro (RecipeDetailLevel){
                  param(recipeBasicStructure){
                    expression (item)
                  }
                }
              }
            }
          }
        }
      }else-if(size(action.recipeCommitState.recipeBasicStructures) == 1){        
        layout-macro (RecipeOneImage){
          param(recipeBasicStructure){
            expression (action.recipeCommitState.recipeBasicStructures[0])
          }
        }

        layout-macro (RecipeOneHeader){
          param(recipeBasicStructure){
            expression (action.recipeCommitState.recipeBasicStructures[0])
          }
        }
      }else{
        section{
          content{
            title-area{
              halign (Start)
              slot1{
                single-line{
                  text{
                    value("결과가 없습니다")
                    style(Title_M)
                  }
                }
              }
            }
          } 
        }
      }  
    }
  }

  conversation-drivers{
    if(action.recipeCommitState.searchType == 'INGREDIENT'){
      conversation-driver{
        template ("쇠고기랑 미역 추가해줘")   
      }
      conversation-driver{
        template ("돼지고기랑 양파 빼줘")   
      }      
    }
    if(size(action.recipeCommitState.recipeBasicStructures) == 1){
      conversation-driver{
        template("뒤로")
      }      
    }
  }
}